Prog      = [Ws Def *(";" Ws Def)] [";"] Ws
            [Ws Actor *(";" Ws Actor)] [";"] Ws
Def       = Type / Import
Type      = "type" Sp Id Sp "=" Sp DataType
Import    = "import" Sp Text
Actor     = "service" [Sp] [Id Sp] ":" Sp [TupType Sp "->" Ws] (ActorType / Id)

ActorType = "{" Ws [MethType *(";" Ws MethType) [";"] Ws] "}"
MethType  = Name [Sp] ":" Ws (FuncType / Id)
FuncType  = TupType [Sp "->" Ws TupType [Sp FuncAnn]]
FuncAnn   = "oneway" / "query"
TupType   = "(" Ws [ArgType *("," Sp ArgType) ["," Ws]] Ws ")" / ArgType
ArgType   = [Name [Sp] ":" Sp] DataType
FieldType = [(Nat / Name) [Sp] ":" Sp] DataType / Nat / Name
DataType  = ConsType / RefType / PrimType / Id

PrimType    = NumType
            / "bool" / "text" / "null" / "reserved" / "empty"
NumType     = "nat8" / "nat16" / "nat32" / "nat64" / "nat"
            / "int8" / "int16" / "int32" / "int64" / "int"
            / "float32" / "float64"

ConsType    = Blob
            / Opt
            / Vec
            / Record
            / Variant

Blob      = "blob"
Opt       = "opt" Sp DataType
Vec       = "vec" Sp DataType
Record    = "record" [Sp] "{" Ws [Fields] Ws "}"
Variant   = "variant" Sp "{" Ws [Fields] Ws "}"
Fields    = FieldType *(";" Ws FieldType) [";"]

RefType   = Func / Service / Principal
Func      = "func" [Sp] FuncType
Service   = "service" Sp ActorType
Principal = "principal"

Name  = Id / Text
Id    = (letter / "_") *(letter / digit / "_")
Text  = %x22 *Char %x22 ; "

Char    = Utf
        / ESC 2*2hex
        / ESC escape
        / "\u{" HexNum "}"
Num     = digit *(["_"] digit)
HexNum  = hex *(["_"] hex)
Nat     = "0x" HexNum / Num

Utf       = ascii / UtfEnc
UtfEnc    = %xC2-DF utfcont
          / %xE0 %xA0-BF utfcont
          / %xED %x80-9F utfcont
          / %xE1-EC 2*2utfcont
          / %xEE-EF 2*2utfcont
          / %xF0 %x90-BF 2*2utfcont
          / %xF4 %x80-8F 2*2utfcont
          / %xF1-F3 3*3utfcont
utfcont   = %x80-BF

ascii    = %x20-21 / %x23-5B / %x5D-7E
escape   = "n" / "r" / "t"
          / ESC
          / %x22 ; "
          / %x27 ; '

letter  = %x41-5A / %x61-7A ; A-Z / a-z
digit   = %x30-39 ; 0-9
hex     = digit / %x41-46 / %x61-66 ; A-F / a-f

CommentText = *(ascii / %x22 / %x27 / %x60) ; " ' `
Comment     = "//" CommentText Nl

Nl   = %x0A / %x0D / (%x0D %x0A)
Ws   = *(Sp / %x09 / Comment / Nl)
Sp   = 1*" "
ESC  = %x5C ; \
